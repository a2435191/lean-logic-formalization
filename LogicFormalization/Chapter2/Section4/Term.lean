import LogicFormalization.Chapter2.Section3.Basic
import LogicFormalization.Chapter2.Section4.Var

universe u v w
variable [HasVar]

inductive Term (L: Language)
/-- `var v` represents the variable `v`, viewed as a word of length `1`-/
| var (v: Var)
/-- `app F ts` represents the concatenation `Ftâ‚...tâ‚™`-/
| app (F: L.Ï) (ts: Fin (arity F) â†’ Term L)


example : Term Language.Ring :=
  .app .mul fun | 0 => .app .add fun
                  | 0 => .var x
                  | 1 => .app .neg fun
                    | 0 => .var y
                | 1 => .var z

namespace Term

variable {L: Language}

/-- Predicate for a variable occurring in a term. -/
def occursIn (v: Var) : Term L â†’ Prop
| .var v' => v = v'
| .app _ ts => âˆƒ i, occursIn v (ts i)

lemma occursIn_app {v: Var} {F} {ts: Fin (arity F) â†’ Term L}
    {i} (h: occursIn v (ts i)): occursIn v (.app F ts) :=
  âŸ¨i, hâŸ©

lemma occursIn_of_occursIn_app {v: Var} {F} {ts: Fin (arity F) â†’ Term L}
    (h: occursIn v (.app F ts)): âˆƒ i, occursIn v (ts i) :=
  h

instance instDecidableOccursIn (v) (t: Term L) :
    Decidable (occursIn v t) :=
  match t with
  | .var v' => if h: v = v' then .isTrue h else .isFalse h
  | .app _F ts =>
    have: âˆ€ i, Decidable (occursIn v (ts i)) := fun i =>
      instDecidableOccursIn v (ts i)
    match h: Fin.find (fun i => occursIn v (ts i)) with
    | some _ =>
      have := (Fin.find_eq_some_iff.mp h).left
      .isTrue (occursIn_app this)
    | none =>
      have := Fin.find_eq_none_iff.mp h
      .isFalse fun âŸ¨i, hiâŸ© => this i hi

/- N.B.: Here, we use an injective mapping from `Fin m` instead of a `Finset Var` so
that we can easily talk about the index of a variable (see `idx`),
which is used in `interp`. -/

/-- `AreVarsFor x t` means that `t = t(xâ‚, ..., xâ‚˜)`, i.e. the `xáµ¢` are
unique and contain (possibly not strictly) all the variables in `t`. -/
structure AreVarsFor {m: â„•} (x: Fin m â†’ Var) (t: Term L) where
  inj': Function.Injective x
  occursIn': âˆ€ {v}, occursIn v t â†’ v âˆˆ Set.range x

namespace AreVarsFor

variable {t: Term L} {m: â„•}

/-- If all the variables in a function application are in `x`, then
then the variables in each subterm remain in `x`. -/
@[simp]
lemma ofApp {F} {m} {ts: Fin (arity F) â†’ Term L} {x: Fin m â†’ Var} (hx: AreVarsFor x (app F ts)):
    (i: Fin (arity F)) â†’ AreVarsFor x (ts i) :=
  fun _i => âŸ¨hx.inj', fun h => hx.occursIn' (occursIn_app h)âŸ©

/-- The (unique) `index` of a variable `xáµ¢` in a set `xâ‚, ..., xâ‚˜` is just `i`. -/
def index {t: Term L} {v: Var} {x: Fin m â†’ Var}
    (hx: AreVarsFor x t) (h: occursIn v t): { i : Fin m // x i = v } :=
  match t with
  | .var _v' =>
    have hi := Fin.isSome_find_iff.mpr (hx.occursIn' h)
    let i := (Fin.find (fun i => x i = v)).get hi
    âŸ¨i, Fin.find_spec _ (Option.get_mem hi)âŸ©
  | .app _F ts =>
    match hj: Fin.find (fun j => occursIn v (ts j)) with
    | some j =>
      index (.ofApp hx j) (Fin.find_eq_some_iff.mp hj).left
    | none => False.elim <|
      have := Fin.find_eq_none_iff.mp hj
      this h.choose h.choose_spec

end AreVarsFor

variable {A: Type u} [Nonempty A]

/-- The interpretation `t^ð’œ` of a term `t` with variables `xâ‚, ..., xâ‚˜`
(which may not actually appear in `t`) is a function from `A^m` to `A`. -/
def interp (t: Term L) (ð’œ: Structure L A) {m} {x: Fin m â†’ Var} (hx: AreVarsFor x t):
    (Fin m â†’ A) â†’ A :=
  match t with
  | .var _xáµ¢ => fun as => as (hx.index rfl)
  | .app F ts => fun as => (F^ð’œ) (fun i =>
    interp (ts i) ð’œ (.ofApp hx i) as)

open Structure in
/-- If `ð’œ âŠ† â„¬` are `L`-structures and `t` is an `L`-term with variables `xâ‚, ..., xâ‚˜`,
then for all `a âˆˆ A^m`, `t^ð’œ(a) = t^â„¬(a)`. -/
lemma interp_substructure {B: Type v} {A: Set B} [Nonempty A]
    {ð’œ: Structure L A} {â„¬: Structure L B}
    (h: ð’œ âŠ† â„¬) {t: Term L} {m} {x: Fin m â†’ Var} (hx: AreVarsFor x t):
    âˆ€ (a: Fin m â†’ A), interp t ð’œ hx a = interp t â„¬ hx (a Â·) :=
  match t with
  | .var v => fun a => rfl
  | .app F ts => fun a => by
    simp only [interp, h.hâ‚‚]
    congr 1
    funext i
    apply interp_substructure
    exact substructure_is_substructure

/-- "A term is said to be *variable-free* if no variables occur in it." -/
def varFree : Term L â†’ Prop
| .var _ => False
| .app _ ts => âˆ€ i, varFree (ts i)

@[simp]
lemma not_varFree_var {v: Var}: Â¬varFree (.var v: Term L) :=
  False.elim

@[simp]
lemma varFree_of_varFree_app {F} {ts: Fin (arity F) â†’ Term L}
    (h: varFree (.app F ts)): âˆ€ {i}, varFree (ts i) :=
  @h

@[simp]
lemma varFree_iff: âˆ€ {t: Term L}, varFree t â†” âˆ€ v, Â¬occursIn v t
| .var _ => by simp [varFree, occursIn]
| .app F ts => by
  simp only [varFree, occursIn, not_exists, varFree_iff]
  apply forall_comm

def AreVarsFor.empty {t: Term L} (ht: varFree t): AreVarsFor Fin.elim0 t where
  inj' := Function.injective_of_subsingleton _
  occursIn' := by simp [varFree_iff.mp ht _]

/-- If `t` is variable-free, we can identify its
interpretation with a single value. -/
def interpConst (t: Term L) (ht: varFree t) (ð’œ: Structure L A): A :=
  interp t ð’œ (.empty ht) Fin.elim0

/-- The more natural way of expressing `interpConst`.
See `interpConst_eq_spec`. -/
def interpConst.spec (t: Term L) (ht: varFree t) (ð’œ: Structure L A): A :=
  match t with
  | .var _ => False.elim ht
  | .app F ts =>
    (F^ð’œ) (fun i => spec (t := ts i) (ht i) ð’œ)

lemma interpConst_eq_spec {t: Term L} (ht: varFree t) {ð’œ: Structure L A} :
    interpConst t ht ð’œ = interpConst.spec t ht ð’œ :=
  match t with
  | .var _ => rfl
  | .app F ts => by
    unfold interpConst interpConst.spec interp
    simp only [â†interpConst_eq_spec]
    rfl

/-- `replace t Ï„ x` is `t(Ï„â‚/xâ‚, ..., Ï„â‚™/xâ‚™)`. Note that `x` must be injective. -/
def replace (t: Term L) {m} (Ï„: Fin m â†’ Term L) (x: Fin m â†ª Var): Term L :=
  match t with
  | .var v =>
    match Fin.find (fun j => x j = v) with
    | none => .var v
    | some j => Ï„ j
  | .app F ts => .app F (fun i => replace (ts i) Ï„ x)

/-- Lemma 2.4.1 -/
theorem replace_varFree {t: Term L} {m} {Ï„: Fin m â†’ Term L}
    {x: Fin m â†’ Var} (hx: AreVarsFor x t) (h: âˆ€ i, varFree (Ï„ i)):
    varFree (replace t Ï„ âŸ¨x, hx.inj'âŸ©) :=
  match t with
  | .var v =>
    match h': Fin.find fun j => x j = v with
    | none =>
      have âŸ¨i, hiâŸ© := hx.occursIn' rfl
      have := Fin.find_eq_none_iff.mp h'
      absurd hi (this i)
    | some j => by
      simp only [replace, Function.Embedding.coeFn_mk, h']
      apply h
  | .app F ts =>
    fun i => replace_varFree (.ofApp hx i) h

-- TODO: generators, examples, (maybe) notation
